//技能ThePusher效果
function ImpulsorKnockBack takes nothing returns nothing
    local timer t=GetExpiredTimer()
    local unit u=GUbyS(TID(t),"u")
    local unit v=GUbyS(TID(t),"v")
    local unit w
    local real dmg=GRbyS(TID(t),"dmg")
    local real dis=GRbyS(TID(t),"dis") //最大击退距离
    local real rdis=GRbyS(TID(t),"rdis") //已经击退距离
    local real ang = GRbyS(TID(t),"ang") //子弹方向
    local real ang2 //击退方向
    local group g=GGbyS(TID(t),"g")
    local group alg=GGbyS(TID(t),"alg")//储存已经被击中过的单位
    call SetUnitX(v, GetUnitX(v)+30*Cos(ang))
    call SetUnitY(v, GetUnitY(v)+30*Sin(ang))
    call DestroyEffect( AddSpecialEffectTarget("Abilities\\Weapons\\AncientProtectorMissile\\AncientProtectorMissile.mdl", v,"origin") )
    call GroupEnumUnitsInRange(g, GetUnitX(v),GetUnitY(v),100,null)
    call GroupRemoveGroup(alg,g)
    call GroupFilter(g,u,true,true,false)
    if CountUnitsInGroup(g)>0 then  //击中单位
      set w=FirstOfGroup(g)
      call GroupAddUnit(alg,w)
      call UnitDamageTarget(u,w,dmg/2, false, false, ATTACK_TYPE_HERO, DAMAGE_TYPE_MAGIC, WEAPON_TYPE_WHOKNOWS )
      set ang2=YDWEAngleBetweenUnits(v,w)
      call YDWETimerPatternRushSlide( w, ang2, dis/2, 0.50, 0.05, 0, false, false, false, "origin", "Abilities\\Weapons\\AncientProtectorMissile\\AncientProtectorMissile.mdl", "Abilities\\Weapons\\SeaElementalMissile\\SeaElementalMissile.mdl" )
      call GroupClear(g)
    endif
    set rdis=rdis+30
    call SRbyS(TID(t),"rdis",rdis)
    if rdis>=dis then
      call DestroyGroup(g)
      call DestroyGroup(alg)
      call FMS(TID(t))
      call PauseTimer(t)
      call DestroyTimer(t)
    endif
    set t=null
    set u=null
    set v=null
    set w=null
    set g=null
    set alg=null
endfunction

function ImpulsorEffect takes nothing returns nothing
    local timer t=GetExpiredTimer()
    local unit u=GUbyS(TID(t),"u")
    local unit bullet=GUbyS(TID(t),"bullet")
    local unit v
    local real dmg=GRbyS(TID(t),"dmg")
    local real dis=GRbyS(TID(t),"dis")
    local real ang=GRbyS(TID(t),"ang")
    local group g=GGbyS(TID(t),"g")
    local real range=GRbyS(TID(t),"range")
    local real route=GRbyS(TID(t),"route") //读取数据
    call SetUnitX(bullet, GetUnitX(bullet)+120*Cos(ang))
    call SetUnitY(bullet, GetUnitY(bullet)+120*Sin(ang))
    set route=route+120
    call SRbyS(TID(t),"route",route)            //移动子弹
    call GroupEnumUnitsInRange(g, GetUnitX(bullet),GetUnitY(bullet),75,null)
    call GroupFilter(g,u,true,true,false)
    if CountUnitsInGroup(g)>0 or route >= range then  //判定碰撞或射程
      call FMS(TID(t))
      call KillUnit(bullet)
      call PauseTimer(t)
      call DestroyTimer(t)
      if CountUnitsInGroup(g)>0 then //判定碰撞
        set v=FirstOfGroup(g)
        call UnitDamageTarget(u,v,dmg, false, false, ATTACK_TYPE_HERO, DAMAGE_TYPE_MAGIC, WEAPON_TYPE_WHOKNOWS )
        set t=CreateTimer()//开启击退计时器
        call SUbyS(TID(t),"u",u)
        call SUbyS(TID(t),"v",v)
        call SRbyS(TID(t),"dmg",dmg)
        call SRbyS(TID(t),"dis", dis)
        call SRbyS(TID(t),"ang",ang)
        call SGbyS(TID(t),"g",g)
        call SGbyS(TID(t),"alg", CreateGroup())
        call TimerStart(t,0.05,true,function ImpulsorKnockBack)
      endif
      call GroupClear(g)
    endif
    set t=null
    set u=null
    set bullet=null
    set v=null
    set g=null
endfunction

// 技能The Pusher 启动
function Trig_ImpulsorRechargingConditions takes nothing returns boolean
    return ((GetSpellAbilityId() == 'A01X'))
endfunction

function ImpulsorTimer takes nothing returns nothing
    local timer t=GetExpiredTimer()
    local unit u=GUbyS(TID(t),"u")
    local real r=GRbyS(TID(t),"r")
    local real ang=GRbyS(TID(t),"ang")
    if r<4 then
      set r=r+0.1
      call SRbyS(TID(t),"r",r)
    endif
    if  OrderId2String(GetUnitCurrentOrder(u)) != "channel" then //中断计时
      if OrderId2String(GetUnitCurrentOrder(u)) == "roar" then //判断是否因为发动“释放”中断计时
        call SRbyS(UID(u),"time",r)
        call SRbyS(UID(u),"ang",ang)
        call SSbyS(UID(u),"ability", "impulsor")
      endif
      call PauseTimer(t)
      call DestroyTimer(t)
      call FMS(TID(t))
    endif
    set t=null
    set u=null
endfunction


function Trig_ImpulsorRechargingActions takes nothing returns nothing
    local unit u=GetTriggerUnit()
    local timer t=CreateTimer()
    local real ang=Atan2(GRbyS(UID(u),"tlocy")-GetUnitY(u),GRbyS(UID(u),"tlocx")-GetUnitX(u))
    //根据ChaikaTargetLoc的储存计算方向（弧度）
    call IssueImmediateOrder( u, "channel" ) //开始蓄力
    call UnitAddAbility(u,'A01Z')
    call SUbyS(TID(t),"u",u)
    call SRbyS(TID(t),"r",0)
    call SRbyS(TID(t),"ang",ang)
    call TimerStart(t,0.1,true,function ImpulsorTimer) //开始蓄力计时
    set u=null
    set t=null
endfunction

//===========================================================================
function InitTrig_ImpulsorRecharging takes nothing returns nothing
    set gg_trg_ImpulsorRecharging = CreateTrigger()
#ifdef DEBUG
    call YDWESaveTriggerName(gg_trg_ImpulsorRecharging, "ImpulsorRecharging")
#endif
    call TriggerRegisterAnyUnitEventBJ( gg_trg_ImpulsorRecharging, EVENT_PLAYER_UNIT_SPELL_FINISH )
    call TriggerAddCondition(gg_trg_ImpulsorRecharging, Condition(function Trig_ImpulsorRechargingConditions))
    call TriggerAddAction(gg_trg_ImpulsorRecharging, function Trig_ImpulsorRechargingActions)
endfunction
