// TheReaper效果
function ReaperEffect takes nothing returns nothing
    local timer t=GetExpiredTimer()
    local unit u=GUbyS(TID(t),"u")
    local unit bullet=GUbyS(TID(t),"bullet")
    local unit v
    local real dmg=GRbyS(TID(t),"dmg")
    local real ang=GRbyS(TID(t),"ang")
    local group g=GGbyS(TID(t),"g")
    local real range=GRbyS(TID(t),"range")
    local real route=GRbyS(TID(t),"route") //读取数据
    local integer i=0
    call SetUnitX(bullet, GetUnitX(bullet)+120*Cos(ang))
    call SetUnitY(bullet, GetUnitY(bullet)+120*Sin(ang))
    set route=route+120
    call SRbyS(TID(t),"route",route)            //移动子弹
    call GroupEnumUnitsInRange(g, GetUnitX(bullet),GetUnitY(bullet),75,null)
    call GroupFilter(g,u,true,true,false)
    if CountUnitsInGroup(g)>0 or route >= range then  //判定碰撞或射程
      call FMS(TID(t))
      call KillUnit(bullet)
      call PauseTimer(t)
      call DestroyTimer(t)
      if CountUnitsInGroup(g)>0 then
        set v=FirstOfGroup(g)
        call UnitDamageTarget(u,v,dmg, false, false, ATTACK_TYPE_HERO, DAMAGE_TYPE_MAGIC, WEAPON_TYPE_WHOKNOWS )
        loop
            set i=i+1
            set t=CreateTimer()
            set bullet=CreateUnit(GetOwningPlayer(u), 'h00H' , GetUnitX(v)+100*Cos(ang), GetUnitY(v)+100*Sin(ang), (ang +Pow(-1, i+1)*bj_PI/6)*180/bj_PI )
            call SUbyS(TID(t),"u",u)
            call SUbyS(TID(t),"bullet",bullet)
            call SRbyS(TID(t),"dmg",dmg*0.75)
            call SRbyS(TID(t),"ang",ang +Pow(-1, i+1)*bj_PI/6)
            call SGbyS(TID(t),"g",CreateGroup())
            call SRbyS(TID(t),"range",range*0.75)
            call TimerStart(t, 0.05, true, function ReaperEffect) //保存数据，开启计时器
            exitwhen i==2
        endloop
        call GroupClear(g)
      endif
    endif
    set t=null
    set u=null
    set bullet=null
    set v=null
    set g=null
endfunction

//TheReaper蓄力
function Trig_TheReaperRechargingConditions takes nothing returns boolean
    return ((GetSpellAbilityId() == 'A023'))
endfunction

function TheReaperTimer takes nothing returns nothing
    local timer t=GetExpiredTimer()
    local unit u=GUbyS(TID(t),"u")
    local real r=GRbyS(TID(t),"r")
    local real ang=GRbyS(TID(t),"ang")
    if r<5 then
      set r=r+0.1
      call SRbyS(TID(t),"r",r)
    endif
    if  OrderId2String(GetUnitCurrentOrder(u)) != "channel" then //中断计时
      if OrderId2String(GetUnitCurrentOrder(u)) == "roar" then //判断是否因为发动“释放”中断计时
        call SRbyS(UID(u),"time",r)
        call SRbyS(UID(u),"ang",ang)
        call SSbyS(UID(u),"ability", "reaper")
      endif
      call PauseTimer(t)
      call DestroyTimer(t)
      call FMS(TID(t))
    endif
    set t=null
    set u=null
endfunction

function Trig_TheReaperRechargingActions takes nothing returns nothing
    local unit u=GetTriggerUnit()
    local timer t=CreateTimer()
    local real ang=Atan2(GRbyS(UID(u),"tlocy")-GetUnitY(u),GRbyS(UID(u),"tlocx")-GetUnitX(u))
    call IssueImmediateOrder( u, "channel" )
    call UnitAddAbility(u,'A01Z')
    call SUbyS(TID(t),"u",u)
    call SRbyS(TID(t),"r",0)
    call SRbyS(TID(t),"ang",ang)
    call TimerStart(t,0.1,true,function TheReaperTimer)
    set u=null
    set t=null
endfunction

//===========================================================================
function InitTrig_TheReaperRecharging takes nothing returns nothing
    set gg_trg_TheReaperRecharging = CreateTrigger()
#ifdef DEBUG
    call YDWESaveTriggerName(gg_trg_TheReaperRecharging, "TheReaperRecharging")
#endif
    call TriggerRegisterAnyUnitEventBJ( gg_trg_TheReaperRecharging, EVENT_PLAYER_UNIT_SPELL_FINISH )
    call TriggerAddCondition(gg_trg_TheReaperRecharging, Condition(function Trig_TheReaperRechargingConditions))
    call TriggerAddAction(gg_trg_TheReaperRecharging, function Trig_TheReaperRechargingActions)
endfunction
